Game:
  "game" name=STRING
  (description=STRING)?
  player=Player
  items*=Item
  levels+=Level
  settings*=Setting
;

Player:
  "player" "{"
    avatar=Avatar
    "height" height=INT
    "width" width=INT
  "}"
;

Avatar:
  ( Image | Color)
;

Image:
  "image" path=STRING
;

Color:
  "color" color=WORD
;

Item:
  Boost | Point
;

Boost:
  "boost" name=ID "{"
    ( Image | Color)
  "}"
;

Point:
  "point" "{"
    ( Image | Color)
  "}"
;

Level:
  "level" name=ID "{"
    background=Background
    platforms+=Platform
  "}"
;

Background:
  "background" ( Image | Color )
;

Platform:
  "platform" "{"
    "position" x=INT y=INT
    "height" height=INT
    "width" width=INT
  "}"
;

Setting:
  "settings" "{"
    ("title" title=STRING |
     "fps" fps=INT |
     "default color" default_color=STRING |
     "font name" font=STRING |
     "screen width" screen_width=INT |
     "screen height" screen_height=INT
    )+

  "}"
;

WORD:
  INT|/[-\w]*\b/
;