Game:
  "game" name=STRING
  (description=STRING)?
  player=Player
  items*=Item
  levels+=Level
  (sounds=Sound)?
  (settings=Setting)?
;

Player:
  "player" "{"
    avatar=Avatar
    "height" height=INT
    "width" width=INT
  "}"
;

Avatar:
  ( ImageAvatar | Color)
;

ImageAvatar:
  default=Image
  ("walk" walkingImage=Image)?
  ("idle" idleImage=Image)?
  ("jump" jumpingImage=Image)?
;

Image:
  "image" path=STRING
;

Color:
  "color" color=WORD
;

Item:
  Boost | Point
;

Boost:
  "boost" name=ID "{"
    avatar=ImageOrColor
  "}"
;

Point:
  "point" name=ID "{"
    avatar=ImageOrColor
  "}"
;

Level:
  "level" name=ID "{"
    "background" background=ImageOrColor
    platforms*=Platform
    items*=ItemReference
  "}"
;

Platform:
  "platform" "{"
    "position" x=INT y=INT
    ("texture" texture=Image)?
    "height" height=INT
    "width" width=INT
  "}"
;

ItemReference:
  BoostInstance | PointInstance
;

BoostInstance:
  "boost" type=[Boost] "{"
    "position" x=INT y=INT
  "}"
;

PointInstance:
  "point" type=[Point] "{"
    "position" x=INT y=INT
  "}"
;

Sound:
  "sounds" "{"
    ("game music" game_music=STRING |
     "jump sound" jump_sound=STRING |
     "boost sound" boost_sound=STRING |
     "point sound" point_sound=STRING |
     "end music" end_music=STRING
    )+
  "}"
;

Setting:
  "settings" "{"
    ("title" title=STRING |
     "fps" fps=INT |
     "default color" default_color=STRING |
     "default texture color" default_texture_color=STRING |
     "font name" font=STRING |
     "screen width" screen_width=INT |
     "screen height" screen_height=INT |
     "movespeed" movespeed=INT
    )+

  "}"
;

ImageOrColor:
  Image | Color
;

WORD:
  INT|/[-\w]*\b/
;